// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}


plugins {
    id "org.sonarqube" version "2.5"
}

apply plugin: 'org.sonarqube'

sonarqube {
    properties {
        property 'sonar.projectName', 'My Super Cool Project'
        property 'sonar.projectVersion', System.getenv('BUILD_NUMBER') ?: 'DEV'
        property 'sonar.branch', System.getenv('SONAR_BRANCH') ?: 'DEV'

        property 'sonar.java.source', JavaVersion.VERSION_1_7
        property 'sonar.java.target', JavaVersion.VERSION_1_7
        property 'sonar.sourceEncoding', 'UTF-8'

        Set<File> sources = []
        Set<File> binaries = []
        Set<File> libraries = []
        Set<File> testSources = []

        def debugVariantData = getVariantData('debug')
        debugVariantData.variantConfiguration.sortedSourceProviders.each { sourceProvider ->
            sources += sourceProvider.assetsDirectories
            sources += sourceProvider.javaDirectories
            sources += sourceProvider.resDirectories
            sources += sourceProvider.resourcesDirectories
        }
        // Replace javaCompileTask with javacTask if upgraded to Android Gradle Plugin 1.3.x
        binaries += debugVariantData.javaCompileTask.outputs.files.files
        libraries += debugVariantData.javaCompileTask.classpath.files
        libraries += getAppPlugin().androidBuilder.bootClasspath

        getVariantData('debugUnitTest').variantConfiguration.sortedSourceProviders.each { sourceProvider ->
            // Only care about the java sources for test variant
            testSources += sourceProvider.javaDirectories
        }
        getVariantData('debugAndroidTest').variantConfiguration.sortedSourceProviders.each { sourceProvider ->
            // Only care about the java sources for androidTest variant
            testSources += sourceProvider.javaDirectories
        }

        property 'sonar.sources', sources.findAll { it.exists() }
        property 'sonar.exclusions', '**/*.js,**/*.css,**/*.html'
        property 'sonar.tests', testSources.findAll { it.exists() }
        property 'sonar.java.binaries', binaries.findAll { it.exists() }
        property 'sonar.java.libraries', libraries.findAll { it.exists() }

        property 'sonar.junit.reportsPath', "${buildDir}/allJunit"
        property 'sonar.jacoco.reportMissing.force.zero', true
        property 'sonar.jacoco.reportPath', "${buildDir}/jacoco/testDebug.exec"
        property 'sonar.jacoco.itReportPath', "${buildDir}/outputs/code-coverage/connected/coverage.ec"
    }
}


allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
